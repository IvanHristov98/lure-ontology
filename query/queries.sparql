# ==== Reasoning demo with inverse property ====
# ==== Selects individuals ====
# Selects all lures that have caught trouts.

PREFIX lure: <http://localhost/lure.rdf#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX fish: <http://localhost/fish.rdf#>
SELECT DISTINCT ?lure ?species WHERE { 
	?lure rdf:type lure:Lure .
    ?lure catch:lureHasCatch ?fish .
    ?fish catch:ofFish fish:Trout
}


# ==== Reasoning demo with transitive property ====
# ==== Selects classes ====
# Selects all material classes that are natural.
# Without reasoning it would output only the direct children of mat:NaturalMaterial.

PREFIX mat: <http://localhost/material.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?natMat WHERE { 
	?natMat rdfs:subClassOf mat:NaturalMaterial
}

# ==== Reasoning demo with inverse property ====
# ==== Selects individuals ====
# ==== Demo for query with > 1 concept and more than 1 FILTER ====
# ==== Demo for query using set operations (UNION) ====
# Selects all of the lures with their respective materials which 
# are heavier than 10 grams and longer than 9 centimeters
# either made of wood or a fusion (e.g. steel).

PREFIX mat: <http://localhost/material.rdf#>
PREFIX lure: <http://localhost/lure.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://localhost/school.rdf#>
SELECT DISTINCT ?lure ?material WHERE { 
	?material lure:bodyMaterialOf ?lure .
    ?lure lure:hasWeight ?weight .
    FILTER (?weight > 10) .
    ?lure lure:hasLength ?length .
    FILTER (?length > 9)
    ?material rdf:type ?matType .
    { ?matType rdfs:subClassOf mat:Wood }
    UNION
    { ?matType rdfs:subClassOf mat:Fusion }
}

# ==== Demo for query with > 1 concept and more than 1 FILTER ====
# ==== Selects individuals ====
# Selects silicone lures that had jig heads with a weight of 7 grams.

PREFIX mat: <http://localhost/material.rdf#>
PREFIX lure: <http://localhost/lure.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://localhost/school.rdf#>
SELECT DISTINCT ?lure WHERE { 
    ?lure rdf:type lure:Lure .
    FILTER EXISTS { ?lure lure:hasBodyMaterial mat:Silicone } .
    ?lure lure:hasHook ?hook .
    ?hook rdf:type lure:Hook .
    ?hook lure:hasJigHead ?jigHead .
    ?jigHead lure:hasJigWeight ?jigWeight .
    FILTER (?jigWeight = 7)
}


# ==== Demo for query with > 1 concept and more than 1 FILTER ====
# ==== Demo for query using set operations (UNION) ====
# ==== Selects individuals ====
# Selects all lures that are either made of metal and caught trout 
# or made of wood and caught pike.

PREFIX mat: <http://localhost/material.rdf#>
PREFIX lure: <http://localhost/lure.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://localhost/school.rdf#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX fish: <http://localhost/fish.rdf#>
SELECT DISTINCT ?lure WHERE { 
    ?catch rdf:type catch:Catch .
    ?catch catch:withLure ?lure .
    ?lure rdf:type lure:Lure .
    ?catch catch:ofFish ?fish .
    ?lure lure:hasBodyMaterial ?bodyMat . 
    ?bodyMat rdf:type ?matType .
    { 
        FILTER EXISTS { ?matType rdfs:subClassOf mat:Metal } .
        FILTER (?fish = fish:Trout)
    }
    UNION
    {
        FILTER EXISTS { ?matType rdfs:subClassOf mat:Wood } .
        FILTER (?fish = fish:Pike)
    }
}

# ==== Demo for BIND ====
# ==== Selects individuals ====
# Selects the names of the fishermen in a pretty way.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://localhost/school.rdf#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
SELECT DISTINCT ?catch ?name WHERE { 
    ?catch rdf:type catch:Catch .
    ?catch catch:caughtBy ?person .
    ?person foaf:givenname ?givenName .
    ?person foaf:family_name ?familyName .
    BIND(CONCAT("sir ", ?givenName, " ", ?familyName, " the relentless") as ?name)
}

# ==== Demo for BIND ====
# ==== Selects individuals ====
# Selects all rods with a power greater than 0.5.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://localhost/school.rdf#>
PREFIX combo: <http://localhost/combo.rdf#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT ?rod ?power WHERE { 
    ?rod rdf:type combo:Rod .
    ?rod combo:hasLength ?rodLength .
    ?rod combo:hasAction ?rodAction .
    BIND(xsd:decimal(?rodLength)*?rodAction AS ?power) .
    FILTER(?power > 0.5)   
}

# ==== Selects classes ====
# Selects the body material types of all lures to have caught trout.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://localhost/school.rdf#>
PREFIX combo: <http://localhost/combo.rdf#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX lure: <http://localhost/lure.rdf#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX fish: <http://localhost/fish.rdf#>
SELECT DISTINCT ?bodyMatType WHERE { 
    ?lure rdf:type lure:Lure .
    ?lure catch:lureHasCatch ?catch .
    ?catch catch:ofFish ?fish .
    FILTER (?fish = fish:Trout) .
    ?lure lure:hasBodyMaterial ?bodyMat .
    ?bodyMat rdf:type ?bodyMatType
}

# Reasons that mat:MallardFlankFeather is a mat:Material.

PREFIX mat: <http://localhost/material.rdf#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?feather WHERE { 
	mat:NaturalMaterial rdfs:subClassOf mat:Material .
    mat:Animal rdfs:subClassOf mat:NaturalMaterial .
    mat:Feather rdfs:subClassOf mat:Animal .
    ?feather rdf:type mat:Feather .
}

# Returns all lures to have caught a trout.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX lure: <http://localhost/lure.rdf#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX fish: <http://localhost/fish.rdf#>
SELECT ?lure ?catch WHERE { 
	?lure rdf:type lure:Lure .
    ?catch catch:ofFish fish:Trout .
    ?catch catch:withLure ?lure .
}

# Finds all schools that were successful in the fishing of trout.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX fish: <http://localhost/fish.rdf#>
PREFIX school: <http://localhost/school.rdf#>
SELECT DISTINCT ?school WHERE { 
	?school rdf:type school:School .
    ?catch catch:ofFish fish:Trout .
    ?catch catch:fromSchool ?school .
}

# Finds the most successful trout fishing school.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX catch: <http://localhost/catch.rdf#>
PREFIX fish: <http://localhost/fish.rdf#>
PREFIX school: <http://localhost/school.rdf#>
SELECT ?school (count(?school) as ?c) WHERE { 
	?school rdf:type school:School .
    ?catch catch:ofFish fish:Trout .
    ?catch catch:fromSchool ?school .
} 
GROUP BY ?school
ORDER BY DESC(?c)
LIMIT 1

# Finds the materials used to make spools.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX mat: <http://localhost/material.rdf#>
PREFIX combo: <http://localhost/combo.rdf#>

SELECT DISTINCT ?material WHERE { 
	?material rdf:type mat:Material .
    ?spool rdf:type combo:Spool .
    ?spool combo:hasSpoolMaterial ?material .
}

# Shows what lures are made out of peacock feathers.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX mat: <http://localhost/material.rdf#>
PREFIX lure: <http://localhost/lure.rdf#>

SELECT DISTINCT ?lure WHERE { 
	?lure rdf:type lure:Lure .
    ?lure lure:hasBodyMaterial mat:PeacockFeather .
}
